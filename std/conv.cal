include "array.cal"
include "ops.cal"

## ## parse_dec
## Parameters: Array* string
##
## Converts decimal integer representation in string into an integer
func parse_dec ptr Array arr -> cell res begin
	let cell pos 0 -> pos
	0 while pos arr.length < do
		pos arr a@
		if dup is_dec_digit then
			'0' - swap 10 * +
		else
			drop break
		end

		pos 1 + -> pos
	end
end

## ## parse_hex
## Parameters: Array* string
##
## Converts hexadecimal integer representation in string into an integer
func parse_hex ptr Array arr -> cell res begin
	let cell pos 0 -> pos
	0 while pos arr.length < do
		pos arr a@
		if dup is_dec_digit then
			'0' - swap 16 * +
		elseif dup 'a' 'f' in_range then
			'a' - 10 + swap 16 * +
		elseif dup 'A' 'F' in_range then
			'A' - 10 + swap 16 * +
		else
			drop break
		end

		pos 1 + -> pos
	end
end
