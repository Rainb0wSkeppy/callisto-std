include "array.cal"

## ## in_range
## Parameters: cell value, u8 first, u8 last
##
## Checks if a value is in a range
func in_range cell value u8 first u8 last -> bool result begin
	value first >= value last <= and
end

## ## (Inline) is_dec_digit
## Parameters: u8 ch
##
## Checks if a character is a decimal digit
inline man is_dec_digit u8 ch -> bool result begin
	'0' '9' in_range
end

## ## (Inline) is_hex_digit
## Parameters: u8 ch
##
## Checks if a character is a hexadecimal digit
inline man is_hex_digit u8 ch -> bool result begin
	dup '0' '9' in_range swap
	dup 'a' 'f' in_range swap
	'A' 'F' in_range or or
end

## ## (Inline) ++
## Parameters: cell num
##
## Returns num + 1
inline man ++ cell num -> cell num++ begin
	1 +
end

## ## (Inline) --
## Parameters: cell num
##
## Returns num - 1
inline man -- cell num -> cell num-- begin
	1 -
end

## ## copy_mem
## Parameters: addr dest, addr src, cell n
##
## Copies n bytes from src to dest
func copy_mem addr dest addr src cell n begin
	let cell i

	while i n < do
		version CallistoScript
			src @ dest !
		end
		version not CallistoScript
			src b@ dest b!
		end
		i ++ -> i
		dest ++ -> dest
		src ++ -> src
	end
end

## ## fill_mem
## Parameters: addr dest, cell n, cell value
##
## Sets n bytes of dest to value
func fill_mem addr dest cell n cell value begin
	let cell i

	while i n < do
		version CallistoScript
			value dest !
		end
		version not CallistoScript
			value dest b!
		end
		i ++ -> i
		dest ++ -> dest
	end
end
